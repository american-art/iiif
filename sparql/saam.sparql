PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX schema: <http://schema.org/>

SELECT ?image (SAMPLE(?rights) AS ?right) (SAMPLE(?descriptions) AS ?description) (SAMPLE(?rights_statements) AS ?rights_statement) (SAMPLE(?uri) AS ?x) (SAMPLE(?captions) AS ?caption)
WHERE {
  GRAPH <http://data.americanartcollaborative.org/saam>
  {
?uri a crm:E22_Man-Made_Object ;
  crm:P138i_has_representation ?image .
?image a crm:E38_Image .

    OPTIONAL {
?uri crm:P102_has_title ?cap .
?cap rdf:value ?captions .
    }
OPTIONAL {
  ?image rdfs:label ?descriptions .
}
OPTIONAL {
  ?image crm:P104_is_subject_to ?rights .
  ?rights a crm:E30_Right .
}
OPTIONAL {
  ?image crm:P67i_is_referred_to_by ?rights_statement_class .
  ?rights_statement_class a crm:E33_Linguistic_Object ;
    crm:P2_has_type <http://vocab.getty.edu/aat/300055547> ;
    rdf:value ?rights_statements .
}
  }
}
GROUP BY ?image